plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'com.tashila.pleasewait'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    viewBinding {
        enabled = true
    }
}

def properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'io.github.tashilapathum'
            artifactId = 'please-wait'
            version = '0.1.2'

            pom {
                name = 'please-wait'
                description = 'A lightweight Material 3 library to replace ProgressDialog from Android'
                url = 'https://github.com/tashilapathum/PleaseWait'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:github.com/tashilapathum/PleaseWait.git'
                    developerConnection = 'scm:git:ssh://github.com/tashilapathum/PleaseWait.git'
                    url = 'https://github.com/tashilapathum/PleaseWait/tree/main'
                }
                developers {
                    developer {
                        id = 'tashilapathum'
                        name = 'Tashila Pathum'
                        email = 'tashila.pathum@gmail.com'
                    }
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}